apply plugin: 'maven-publish'

// publish to private Gradleware repository
publishing {
  repositories {
    maven {
      name = 'privateGradle'
      url gradlePrivateRepositoryUrl
      credentials {
        username GRADLE_REPO_USER
        password GRADLE_REPO_PASSWORD
      }
    }
  }
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar {
        classifier "sources"
      }
    }
  }
}

dependencies {
  compile "org.gradle:gradle-tooling-api:$toolingApiVersion"
  compile "com.google.guava:guava:$guavaVersion"
  compile "org.slf4j:slf4j-api:$slf4jVersion"
  testCompile project(':testing-junit')
  testCompile "org.spockframework:spock-core:$spockVersion"
  testCompile "nl.jqno.equalsverifier:equalsverifier:$equalsverifierVersion"
  testRuntime "cglib:cglib-nodep:$cglibVersion"
  testRuntime "org.objenesis:objenesis:$objenesisVersion"
  testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
}

description = "IDE-agnostic Tooling API component of the Commons project of the Gradle Tooling Platform."

sourceSets {
  sample {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
}

configurations {
  sampleCompile.extendsFrom compile
  sampleRuntime.extendsFrom runtime
}

build.dependsOn sampleClasses
